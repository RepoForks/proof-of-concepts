apply plugin: 'groovy'
apply plugin: 'maven'

group = 'com.github.edsilfer'
version = '1.0.0'

ext {
    test_version_junit = '4.12'
    test_version_spock = '1.1-groovy-2.4'
}

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile gradleTestKit()
    testCompile "junit:junit:$test_version_junit"
    testCompile ("org.spockframework:spock-core:$test_version_spock") {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
}

test {
    outputs.upToDateWhen { false }

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        stackTraceFilters "entryPoint"
    }
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    testRuntime files(createClasspathManifest)
}

sourceCompatibility = 1.7
targetCompatibility = 1.7